AC_PREREQ(2.50)
AC_DEFUN([AC_CHECK_LIB_EXT], [
	AC_CHECK_LIB([$1],[$3],[$4],[$5],[$7])
	ac_cv_lib_ext_$1_$3=$ac_cv_lib_$1_$3
])
AC_DEFUN([AC_CHECK_FUNC_EXT], [
	AC_CHECK_FUNC([$1],[$3],[$4])
	ac_cv_func_ext_$1=$ac_cv_func_$1
])
AC_DEFUN([SMB_MODULE_DEFAULT], [echo -n ""])
AC_DEFUN([SMB_LIBRARY_ENABLE], [echo -n ""])
AC_DEFUN([SMB_EXT_LIB], [echo -n ""])
AC_DEFUN([SMB_ENABLE], [echo -n ""])
AC_INIT(include/ldb.h)
AC_CONFIG_SRCDIR([common/ldb.c])
WITH_GCOV=0
AC_ARG_ENABLE(gcov,
	AS_HELP_STRING([--enable-gcov],[enable GCOV code coverage tests]),
	[ WITH_GCOV=1])
AC_SUBST(WITH_GCOV)
AC_PROG_CC
AC_SYS_LARGEFILE
AC_FUNC_MMAP
AC_PATH_PROG(XSLTPROC,xsltproc)
AC_PATH_PROG(DOXYGEN,doxygen)
AC_PATH_PROG(GCOV,gcov)
AC_CHECK_HEADERS(stdint.h dlfcn.h)
AC_CONFIG_HEADER(include/config.h)
AC_CHECK_FUNCS(dlopen dlsym dlclose)

SHLIBEXT="so" # Should be set based on OS later on
AC_SUBST(SHLIBEXT)

MODULESDIR="$libdir/ldb"
AC_DEFINE(MODULESDIR,$MODULESDIR,[Modules directory])
AC_SUBST(MODULESDIR)

AC_CHECK_LIB(popt, poptGetContext)

sinclude(ldap.m4)
WITH_LDAP=$with_ldap_support
AC_SUBST(WITH_LDAP)
sinclude(sqlite3.m4)
WITH_SQLITE3=$with_sqlite3_support
AC_SUBST(WITH_SQLITE3)
sinclude(config.m4)
sinclude(../talloc/config.m4)
sinclude(../tdb/config.m4)
AC_OUTPUT(Makefile ldb.pc)

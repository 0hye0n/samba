dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision$)
AC_INIT(lib/krb5/send_to_kdc.c)
AM_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST
CANONICAL_HOST=$host
AC_SUBST(CANONICAL_HOST)

PACKAGE=heimdal
VERSION=0.0
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
dnl AC_KRB_PROG_YACC
AC_PROG_YACC
AC_PROG_LEX
AC_PROG_RANLIB
AC_PROG_AWK
AC_PROG_MAKE_SET
AC_LN_S
AC_ARG_PROGRAM

CFLAGS="-g"

berkeley_db=db
AC_ARG_WITH(berkeley-db,
[  --without-berkeley-db   if you don't want berkeley db],[
if test "$withval" = no; then
	berkeley_db=""
fi
])

dnl test

AC_C_BIGENDIAN

dnl AM_C_PROTOTYPES

dnl Checks for libraries.

AC_FIND_FUNC(socket, socket)
AC_FIND_FUNC(gethostbyname, nsl)
AC_FIND_FUNC(dbopen, $berkeley_db)
AC_FIND_FUNC(dbm_firstkey, $berkeley_db gdbm ndbm)
AC_FIND_FUNC(syslog, syslog)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/ioctl.h sys/time.h unistd.h)
AC_CHECK_HEADERS(sys/bitypes.h sys/filio.h sys/types.h)
AC_CHECK_HEADERS(db.h ndbm.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_DECL_YYTEXT

dnl Checks for library functions.
AC_CHECK_FUNCS(gethostname mktime select socket strdup strstr)
AC_CHECK_FUNCS(random rand setitimer)

AC_TYPE_SIGNAL
if test "$ac_cv_type_signal" = "void" ; then
	AC_DEFINE(VOID_RETSIGTYPE, 1)
fi
AC_SUBST(VOID_RETSIGTYPE)

dnl How to figure out where in the timezone world we are

AC_HAVE_STRUCT_FIELD([#include <time.h>], [tm], [int], [tm_gmtoff])
AC_KRB_VAR_TIMEZONE

AC_GROK_TYPES(int8_t int16_t int32_t int64_t)
AC_GROK_TYPES(u_int8_t u_int16_t u_int32_t u_int64_t)

AC_OUTPUT(Makefile 		\
	include/Makefile	\
	lib/Makefile		\
	lib/asn1/Makefile	\
	lib/des/Makefile	\
	lib/krb5/Makefile	\
	lib/krb5/error/Makefile	\
	lib/krb5/error/compile_et \
	lib/hdb/Makefile	\
	kuser/Makefile		\
	kdc/Makefile)

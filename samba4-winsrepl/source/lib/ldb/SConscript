#            tastes like -*- python -*-

Import('hostenv', 'talloc', 'defines', 'basic', 'cli_ldap')

# Configure stuff

if hostenv['configure']:
    conf = Configure(hostenv)
    if conf.CheckLibWithHeader('sqlite3','sqlite3.h','c','sqlite3_open()'):
        defines['HAVE_SQLITE3'] = 1
    conf.CheckLibWithHeader('popt','popt.h','c','poptGetArgs(NULL);')
    conf.Finish()

if defines.has_key('HAVE_SQLITE3'):
    hostenv.Library('ldb_sqlite3/ldb_sqlite3.c')

# LDB modules

modules = []

modules.append(hostenv.Library('modules/timestamps.c'))
modules.append(hostenv.Library('modules/rdn_name.c'))
modules.append(hostenv.Library('modules/schema.c'))
modules.append(hostenv.Library('ldb_ildap/ldb_ildap.c'))
#modules.append(hostenv.Library('modules/ldb_map.c'))

Import('dsdb_ldb_modules')
modules += dsdb_ldb_modules

modules.append(hostenv.Library(
    'ldb_tdb',
    ['ldb_tdb/ldb_tdb.c', 'ldb_tdb/ldb_search.c', 'ldb_tdb/ldb_pack.c',
     'ldb_tdb/ldb_index.c', 'ldb_tdb/ldb_cache.c', 'ldb_tdb/ldb_tdb_wrap.c']))
	       
#modules.append(hostenv.Library('ldb_ildap', ['ldb_ildap/ldb_ildap.c']))

# Export the ldb base plus modules for other parts of the build system
# to enjoy.

ldb = hostenv.Library(
    'ldb',
    ['common/ldb.c', 'common/ldb_ldif.c', 'common/ldb_parse.c',
     'common/ldb_parse.c', 'common/ldb_msg.c', 'common/ldb_utf8.c',
     'common/ldb_debug.c', 'common/ldb_modules.c', 'common/ldb_match.c',
     'common/ldb_attributes.c', 'common/attrib_handlers.c', 'common/ldb_dn.c'])

Export('ldb')

hostenv.Library('samba/ldif_handlers.c')
ldb_cmdline = hostenv.Library('ldb_cmdline', 'tools/cmdline.c')

# Tools

ldbenv = hostenv.Copy()
ldbenv.Append(LIBS = ['popt'])

ldbenv.Program('ldbadd',['tools/ldbadd.c',ldb,modules,ldb_cmdline,cli_ldap,talloc,basic])
ldbenv.Program('ldbdel',['tools/ldbdel.c',ldb,ldb_cmdline,basic])
ldbenv.Program('ldbmodify',['tools/ldbmodify.c',ldb,ldb_cmdline])
ldbenv.Program('ldbsearch',['tools/ldbsearch.c',ldb,ldb_cmdline])
ldbenv.Program('ldbrename',['tools/ldbrename.c',ldb,ldb_cmdline])
ldbenv.Program('ldbtest',['tools/ldbtest.c',ldb,ldb_cmdline])
ldbenv.Program('oLschema2ldif',['tools/oLschema2ldif.c',ldb])
